Процедура ЗаписатьСообщениеСИзменениями(Каталог, выводитьСообщения = Истина) Экспорт
	Если выводитьСообщения Тогда 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Начало выгрузки в узел " + Строка(ЭтотОбъект) + " " + формат(ТекущаяДата(), "ДЛФ=T");
		Сообщение.Сообщить();
	КонецЕсли;
	
	#Если МобильноеПриложениеСервер Тогда
	    Разделитель = "/";
	#Иначе
	    Разделитель = "\";    
	#КонецЕсли
	
	каталогНаДиске = новый Файл(Каталог);
	
	попытка
		Если не каталогНаДиске.Существует() Тогда 
			СоздатьКаталог(Каталог);
		КонецЕсли;
	Исключение
		Сообщить("Ошибка определения каталога " + Каталог + ". " + ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	// Сформировать имя временного файла.
	ИмяФайла = Каталог + ?(Прав(Каталог, 1) = Разделитель, "", Разделитель) + "Message" + СокрЛП(ПланыОбмена.Мобильные.ЭтотУзел().Код) + "_" +
	СокрЛП(Ссылка.Код) + ".xml";
	
	// Создать объект записи XML
	// *** ЗаписьXML-документов.
	ЗаписьXML = Новый ЗаписьXML;
	
	ЗаписьXML.ОткрытьФайл(ИмяФайла);
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	// *** Инфраструктура сообщений.
	ЗаписьСообщения = ПланыОбмена.СоздатьЗаписьСообщения();
	ЗаписьСообщения.НачатьЗапись(ЗаписьXML, Ссылка);
	
	Если выводитьСообщения Тогда 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = " Номер сообщения: " + ЗаписьСообщения.НомерСообщения;
		Сообщение.Сообщить();
	КонецЕсли;
	
	// Получить выборку измененных данных
	// *** Механизм регистрации изменений.
	ВыборкаИзменений = ПланыОбмена.ВыбратьИзменения(ЗаписьСообщения.Получатель,ЗаписьСообщения.НомерСообщения);
	
	количествоИзмененныхОбъектов = 0;
	
	Пока ВыборкаИзменений.Следующий() Цикл
		Данные = ВыборкаИзменений.Получить();
		
		// Записать данные в сообщение *** XML-сериализация.
		ЗаписатьXML(ЗаписьXML, Данные);		
		
		количествоИзмененныхОбъектов = количествоИзмененныхОбъектов + 1;
	КонецЦикла;
	
	ЗаписьСообщения.ЗакончитьЗапись();
	ЗаписьXML.Закрыть();
	
	Если выводитьСообщения Тогда 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Выгружено " + количествоИзмененныхОбъектов + " " + формат(ТекущаяДата(), "ДЛФ=T");
		Сообщение.Сообщить();
		
		Сообщение.Текст = ИмяФайла;
		Сообщение.Сообщить();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПрочитатьСообщениеСИзменениями(Каталог) Экспорт
	#Если МобильноеПриложениеСервер Тогда
	    Разделитель = "/";
	#Иначе
	    Разделитель = "\";    
	#КонецЕсли
	
	// Сформировать имя файла.
	ИмяФайла = Каталог + ?(Прав(Каталог, 1) = Разделитель, "", Разделитель) + "Message" + СокрЛП(Ссылка.Код) + "_" +
	СокрЛП(ПланыОбмена.Мобильные.ЭтотУзел().Код) + ".xml";
	
	Файл = Новый Файл(ИмяФайла);
	Если Не Файл.Существует() Тогда
		Сообщить("Не найден файл: " + ИмяФайла); 
		
		Возврат;		
	КонецЕсли;
	
	// *** Чтение документов XML
	// Попытаться открыть файл.
	ЧтениеXML = Новый ЧтениеXML;
	Попытка
		ЧтениеXML.ОткрытьФайл(ИмяФайла);		
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Невозможно открыть файл обмена данными.";
		Сообщение.Сообщить();
		
		Возврат;		
	КонецПопытки;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Начало загрузки из " + Строка(ЭтотОбъект) + " " + формат(ТекущаяДата(), "ДЛФ=T");
	Сообщение.Сообщить();
	
	Попытка
		// Загрузить из найденного файла
		// *** Инфраструктура сообщений.
		ЧтениеСообщения = ПланыОбмена.СоздатьЧтениеСообщения();
		
		// Читать заголовок сообщения обмена данными – файла XML.
		ЧтениеСообщения.НачатьЧтение(ЧтениеXML, ДопустимыйНомерСообщения.Любой);
		
		// Сообщение предназначено не Для этого узла.
		Если ЧтениеСообщения.Отправитель <> Ссылка Тогда
			ВызватьИсключение "Неверный узел";
			
		КонецЕсли;
		
		// Удаляем регистрацию изменений Для узла отправителя сообщения.
		// *** Служба регистрации изменений.
		ПланыОбмена.УдалитьРегистрациюИзменений(ЧтениеСообщения.Отправитель, ЧтениеСообщения.НомерПринятого);
		
		количествоИзмененныхОбъектов = 0;
		
		// Читаем данные из сообщения *** XML-сериализация.
		Пока ВозможностьЧтенияXML(ЧтениеXML) Цикл
			// Читаем очередное значение.
			Данные = ПрочитатьXML(ЧтениеXML);
			
			// Записать полученные данные.
			Данные.ОбменДанными.Отправитель = ЧтениеСообщения.Отправитель;
			Данные.ОбменДанными.Загрузка = Истина;
			Данные.Записать();
			
			количествоИзмененныхОбъектов = количествоИзмененныхОбъектов + 1;			
		КонецЦикла;
		
		ЧтениеСообщения.ЗакончитьЧтение();
		ЧтениеXML.Закрыть();
		УдалитьФайлы(ИмяФайла);		
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Ошибка чтения XML: " + ОписаниеОшибки();
		Сообщение.Сообщить();
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Последние прочитаные данные: " + строка(Данные);
		Сообщение.Сообщить();
	КонецПопытки;
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Загружено " + количествоИзмененныхОбъектов + " " + формат(ТекущаяДата(), "ДЛФ=T");
	Сообщение.Сообщить();	
КонецПроцедуры