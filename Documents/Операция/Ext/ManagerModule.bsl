#Область СлужебныеПроцедурыИФункции

#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаДолги(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаЗатраты(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаЗатратыОбщие(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаКошельки(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаКошелькиПартии(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПокупкиВалюты(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПродажиВалюты(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСерверУТ.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	//Запрос.Текст =
	//"ВЫБРАТЬ
	//|	ДанныеДокумента.Ссылка КАК Ссылка,
	//|	ДанныеДокумента.ТипОперации КАК ТипОперации,
	//|	ДанныеДокумента.Кошелек КАК Кошелек,
	//|	ДанныеДокумента.Валюта КАК Валюта,
	//|	ДанныеДокумента.Курс КАК Курс,
	//|	ДанныеДокумента.Дата КАК Период
	//|ИЗ
	//|	Документ.Операция КАК ДанныеДокумента
	//|ГДЕ
	//|	ДанныеДокумента.Ссылка = &Ссылка";
	//Реквизиты = Запрос.Выполнить().Выбрать();
	//Реквизиты.Следующий();
	//
	//Запрос.УстановитьПараметр("Период",                                   Реквизиты.Период);
	//Запрос.УстановитьПараметр("Валюта",                                   Реквизиты.Валюта);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаДолги(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "Долги";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	Операция.Дата КАК Период,
	|	Операция.ФизЛицо КАК Физлицо,
	|	Операция.Валюта КАК Валюта,
	|	Операция.Метка КАК Метка,
	|	ВЫБОР
	|		КОГДА Операция.СуммаДолг = 0
	|			ТОГДА Операция.Сумма
	|		ИНАЧЕ Операция.СуммаДолг
	|	КОНЕЦ КАК Сумма
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Долги)
	|	И Операция.ТипДолга В (ЗНАЧЕНИЕ(Перечисление.ТипыДолгов.Дебет), ЗНАЧЕНИЕ(Перечисление.ТипыДолгов.Зачисление))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	Операция.Дата,
	|	Операция.ФизЛицо,
	|	Операция.Валюта,
	|	ЗНАЧЕНИЕ(Справочник.Метки.),
	|	Операция.Сумма - Операция.СуммаДолг
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Долги)
	|	И Операция.ТипДолга В (ЗНАЧЕНИЕ(Перечисление.ТипыДолгов.Дебет), ЗНАЧЕНИЕ(Перечисление.ТипыДолгов.Зачисление))
	|	И Операция.СуммаДолг <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	Операция.Дата КАК Период,
	|	Операция.ФизЛицо КАК Физлицо,
	|	Операция.Валюта КАК Валюта,
	|	Операция.Метка КАК Метка,
	|	ВЫБОР
	|		КОГДА Операция.СуммаДолг = 0
	|			ТОГДА Операция.Сумма
	|		ИНАЧЕ Операция.СуммаДолг
	|	КОНЕЦ КАК Сумма
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Долги)
	|	И Операция.ТипДолга В (ЗНАЧЕНИЕ(Перечисление.ТипыДолгов.Кредит), ЗНАЧЕНИЕ(Перечисление.ТипыДолгов.Списание))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	Операция.Дата,
	|	Операция.ФизЛицо,
	|	Операция.Валюта,
	|	ЗНАЧЕНИЕ(Справочник.Метки.),
	|	Операция.Сумма - Операция.СуммаДолг
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Долги)
	|	И Операция.ТипДолга В (ЗНАЧЕНИЕ(Перечисление.ТипыДолгов.Кредит), ЗНАЧЕНИЕ(Перечисление.ТипыДолгов.Списание))
	|	И Операция.СуммаДолг <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	Операция.Дата,
	|	Операция.ФизЛицо,
	|	Операция.Валюта,
	|	Операция.Метка,
	|	Операция.Сумма
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Долги)
	|	И Операция.ТипДолга = ЗНАЧЕНИЕ(Перечисление.ТипыДолгов.Взаимозачет)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	Операция.Дата,
	|	Операция.ФизЛицо,
	|	Операция.Валюта,
	|	Операция.МеткаОтправитель,
	|	Операция.Сумма
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Долги)
	|	И Операция.ТипДолга = ЗНАЧЕНИЕ(Перечисление.ТипыДолгов.Взаимозачет)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Операция.СуммаОбщиеЗатраты - Операция.Сумма >= Операция.СуммаДолг
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ,
	|	Операция.Дата,
	|	Операция.ФизЛицо,
	|	Операция.Валюта,
	|	Операция.Метка,
	|	Операция.СуммаДолг
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации В (ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Расход), ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Доход))
	|	И Операция.ОбщиеЗатраты
	|	И Операция.СуммаДолг <> 0
	|	И Операция.ФизЛицо <> ЗНАЧЕНИЕ(Справочник.ФизЛица.)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Операция.СуммаОбщиеЗатраты - Операция.Сумма >= Операция.СуммаДолг
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ,
	|	Операция.Дата,
	|	Операция.ФизЛицо,
	|	Операция.Валюта,
	|	Операция.Метка,
	|	Операция.СуммаДолг
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Сторно)
	|	И Операция.ОбщиеЗатраты
	|	И Операция.СуммаДолг <> 0
	|	И Операция.ФизЛицо <> ЗНАЧЕНИЕ(Справочник.ФизЛица.)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаЗатраты(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "Затраты";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Операция.Дата КАК Период,
	|	Операция.Статья КАК Статья,
	|	Операция.Валюта КАК Валюта,
	|	Операция.Метка КАК Метка,
	|	Операция.ФизЛицо КАК ФизЛицо,
	|	Операция.Кошелек КАК Кошелек,
	|	ВЫБОР
	|		КОГДА Операция.ТипДолга = ЗНАЧЕНИЕ(Перечисление.ТипыДолгов.Зачисление)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Доход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Расход)
	|	КОНЕЦ КАК ТипОперации,
	|	ВЫБОР
	|		КОГДА Операция.ТипДолга = ЗНАЧЕНИЕ(Перечисление.ТипыДолгов.Зачисление)
	|			ТОГДА Операция.Сумма
	|		ИНАЧЕ -Операция.Сумма
	|	КОНЕЦ КАК Сумма
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Долги)
	|	И Операция.ТипДолга В (ЗНАЧЕНИЕ(Перечисление.ТипыДолгов.Списание), ЗНАЧЕНИЕ(Перечисление.ТипыДолгов.Зачисление))
	|	И Операция.Статья <> ЗНАЧЕНИЕ(Справочник.Статьи.)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Дата,
	|	Операция.Статья,
	|	Операция.Валюта,
	|	Операция.Метка,
	|	Операция.ФизЛицо,
	|	Операция.Кошелек,
	|	Операция.ТипОперации,
	|	ВЫБОР
	|		КОГДА Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Доход)
	|			ТОГДА Операция.Сумма
	|		ИНАЧЕ -Операция.Сумма
	|	КОНЕЦ
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации В (ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Доход), ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Расход))
	|	И НЕ Операция.ОбщиеЗатраты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Дата,
	|	Операция.Статья,
	|	Операция.Валюта,
	|	Операция.Метка,
	|	Операция.ФизЛицо,
	|	Операция.Кошелек,
	|	Операция.ТипОперации,
	|	ВЫБОР
	|		КОГДА Операция.Основание.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Доход)
	|			ТОГДА -Операция.Сумма
	|		ИНАЧЕ Операция.Сумма
	|	КОНЕЦ
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Сторно)
	|	И НЕ Операция.ОбщиеЗатраты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Дата,
	|	Операция.Статья,
	|	Операция.Валюта,
	|	Операция.Метка,
	|	Операция.ФизЛицо,
	|	Операция.Кошелек,
	|	Операция.ТипОперации,
	|	ВЫБОР
	|		КОГДА Операция.Сумма = 0
	|			ТОГДА -Операция.СуммаДолг
	|		ИНАЧЕ Операция.СуммаДолг - Операция.Сумма
	|	КОНЕЦ
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации В (ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Доход), ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Расход))
	|	И Операция.ОбщиеЗатраты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Дата,
	|	Операция.Статья,
	|	Операция.Валюта,
	|	Операция.Метка,
	|	Операция.ФизЛицо,
	|	Операция.Кошелек,
	|	Операция.ТипОперации,
	|	ВЫБОР
	|		КОГДА Операция.Сумма = 0
	|			ТОГДА Операция.СуммаДолг
	|		ИНАЧЕ Операция.Сумма - Операция.СуммаДолг
	|	КОНЕЦ
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Сторно)
	|	И Операция.ОбщиеЗатраты";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаЗатратыОбщие(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ЗатратыОбщие";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Операция.Дата КАК Период,
	|	Операция.Статья КАК Статья,
	|	Операция.Валюта КАК Валюта,
	|	Операция.Метка КАК Метка,
	|	Операция.ФизЛицо КАК ФизЛицо,
	|	Операция.Кошелек КАК Кошелек,
	|	Операция.ТипОперации КАК ТипОперации,
	|	ВЫБОР
	|		КОГДА Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Доход)
	|			ТОГДА Операция.СуммаОбщиеЗатраты
	|		ИНАЧЕ -Операция.СуммаОбщиеЗатраты
	|	КОНЕЦ КАК Сумма
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации В (ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Доход), ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Расход))
	|	И Операция.ОбщиеЗатраты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Дата,
	|	Операция.Статья,
	|	Операция.Валюта,
	|	Операция.Метка,
	|	Операция.ФизЛицо,
	|	Операция.Кошелек,
	|	Операция.ТипОперации,
	|	ВЫБОР
	|		КОГДА Операция.Основание.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Доход)
	|			ТОГДА -Операция.СуммаОбщиеЗатраты
	|		ИНАЧЕ Операция.СуммаОбщиеЗатраты
	|	КОНЕЦ
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Сторно)
	|	И Операция.ОбщиеЗатраты";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаКошельки(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "Кошельки";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	Операция.Дата КАК Период,
	|	Операция.Валюта КАК Валюта,
	|	Операция.Кошелек КАК Кошелек,
	|	Операция.Сумма КАК Сумма
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Доход)
	|	И Операция.Кошелек <> ЗНАЧЕНИЕ(Справочник.Кошельки.)
	|	И Операция.Сумма <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	Операция.Дата,
	|	Операция.Валюта,
	|	Операция.Кошелек,
	|	Операция.Сумма
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Расход)
	|	И Операция.Кошелек <> ЗНАЧЕНИЕ(Справочник.Кошельки.)
	|	И Операция.Сумма <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	Операция.Дата,
	|	Операция.Валюта,
	|	Операция.Кошелек,
	|	Операция.Сумма
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Сторно)
	|	И Операция.Основание.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Доход)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	Операция.Дата,
	|	Операция.Валюта,
	|	Операция.Кошелек,
	|	Операция.Сумма
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Сторно)
	|	И Операция.Основание.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Расход)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	Операция.Дата,
	|	Операция.Валюта,
	|	Операция.Кошелек,
	|	Операция.Сумма
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Перевод)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	Операция.Дата,
	|	Операция.КошелекПриемник.Валюта,
	|	Операция.КошелекПриемник,
	|	Операция.СуммаПриемник
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Перевод)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	Операция.Дата,
	|	Операция.Валюта,
	|	Операция.Кошелек,
	|	Операция.Сумма
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Долги)
	|	И Операция.ТипДолга = ЗНАЧЕНИЕ(Перечисление.ТипыДолгов.Дебет)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	Операция.Дата,
	|	Операция.Валюта,
	|	Операция.Кошелек,
	|	Операция.Сумма
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОпераций.Долги)
	|	И Операция.ТипДолга = ЗНАЧЕНИЕ(Перечисление.ТипыДолгов.Кредит)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаКошелькиПартии(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "КошелькиПартии";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	1 КАК Поле1
	|ГДЕ
	|	ЛОЖЬ";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПокупкиВалюты(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПокупкиВалюты";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	1 КАК Поле1
	|ГДЕ
	|	ЛОЖЬ";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПродажиВалюты(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПродажиВалюты";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	1 КАК Поле1
	|ГДЕ
	|	ЛОЖЬ";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти