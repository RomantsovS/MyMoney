#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ПодготовитьФормуНаСервере();	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если НачальныйОстаток <> мНачальныйОстаток Тогда 
		запрос = новый запрос("ВЫБРАТЬ
		|	ВводОстатков.Ссылка
		|ИЗ
		|	Документ.ВводОстатков КАК ВводОстатков
		|ГДЕ
		|	ВводОстатков.Кошелек = &Кошелек");
		запрос.УстановитьПараметр("кошелек", Объект.Ссылка);
		
		рез = запрос.Выполнить().Выбрать();
		
		Если рез.Следующий() Тогда 
			новДок = рез.ссылка.получитьОбъект();			
		Иначе
			новДок = Документы.ВводОстатков.СоздатьДокумент();
			новДок.Кошелек = Объект.Ссылка;
			
			запрос = новый запрос("ВЫБРАТЬ ПЕРВЫЕ 1
			|	КошелькиОбороты.Период КАК Период
			|ИЗ
			|	РегистрНакопления.Кошельки.Обороты(, , Регистратор, кошелек = &кошелек) КАК КошелькиОбороты
			|
			|УПОРЯДОЧИТЬ ПО
			|	Период");
			запрос.УстановитьПараметр("кошелек", Объект.Ссылка);
			резКош = запрос.Выполнить().Выбрать();
			
			Если резКош.Следующий() Тогда 
				новДок.Дата = резКош.период - 1;
			Иначе
				новДок.Дата = ТекущаяДата();
			КонецЕсли;
		КонецЕсли;                   
		
		новДок.Сумма = НачальныйОстаток;
		
		новДок.Записать(РежимЗаписиДокумента.Проведение);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ОбновитьОстатки");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВалютаПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Если не ВалютаПроверитьВозможностьИзмененияНаСервере() Тогда 
			ПоказатьПредупреждение(,
			НСтр("ru = 'По данному кошельку имеются операции. Изменение валюты невозможно!'"));
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СделатьОсновным(Команда)
	СделатьОсновнымНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СделатьОсновнымНаСервере()
	Константы.ОсновнойКошелек.Установить(Объект.Ссылка);
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ВводОстатков.Ссылка
	|ИЗ
	|	Документ.ВводОстатков КАК ВводОстатков
	|ГДЕ
	|	ВводОстатков.Кошелек = &Кошелек");
	Запрос.УстановитьПараметр("Кошелек", Объект.Ссылка);
	
	Рез = Запрос.Выполнить().Выбрать();
	
	Если Рез.Следующий() Тогда
		НачальныйОстаток = Рез.Ссылка.Сумма;
		мНачальныйОстаток = НачальныйОстаток;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ВалютаПроверитьВозможностьИзмененияНаСервере()
	запрос = новый запрос("ВЫБРАТЬ ПЕРВЫЕ 1
	|	Операция.Ссылка
	|ИЗ
	|	Документ.Операция КАК Операция
	|ГДЕ
	|	(Операция.Кошелек = &Кошелек
	|			ИЛИ Операция.КошелекПриемник = &Кошелек)");
	запрос.УстановитьПараметр("кошелек", Объект.Ссылка);
	
	рез = запрос.Выполнить();
	
	Если не рез.Пустой() Тогда 
		Объект.Валюта = Объект.Ссылка.Валюта;
		
		Возврат ложь;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

#КонецОбласти