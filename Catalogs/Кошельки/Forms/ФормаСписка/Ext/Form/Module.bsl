#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Список.РежимВыбора      = Параметры.РежимВыбора;
	Элементы.Список.ОтображатьКорень = Параметры.РежимВыбора;
	
	ОсновнойКошелек = Константы.ОсновнойКошелек.Получить();
	
	УстановитьСкрытьНеиспользуемые();
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьОперации(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура("ОтборПоКошельку", Элементы.Список.ТекущиеДанные.ссылка);
	ОткрытьФорму("Документ.Операция.ФормаСписка", ПараметрыОткрытия, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьНеиспользуемые(Команда)
	
	УстановитьСкрытьНеиспользуемые();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	НовЭлем = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = НовЭлем.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Список.Имя);
	
	ОтборЭлемента = НовЭлем.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Ссылка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("ОсновнойКошелек");
	
	НовЭлем.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(, , Истина));
	НовЭлем.Использование = Истина;
	
	НовЭлем = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = НовЭлем.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Список.Имя);
	
	ОтборЭлемента = НовЭлем.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.НеИспользуется");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	НовЭлем.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Серый);
	НовЭлем.Использование = Истина;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСкрытьНеиспользуемые()
	
	СкрытьНеиспользуемыеПараметр = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(Список.Параметры.Элементы,
	"СкрытьНеиспользуемые");
	
	Если СкрытьНеиспользуемыеПараметр <> Неопределено Тогда 
		СкрытьНеиспользуемые = СкрытьНеиспользуемыеПараметр.Значение <> Истина;
	Иначе
		СкрытьНеиспользуемые = Истина;
	КонецЕсли;
	
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(Список.Параметры.Элементы, "СкрытьНеиспользуемые",
	СкрытьНеиспользуемые);
	
	Если СкрытьНеиспользуемые Тогда 
		Заголовок = НСтр("ru='Показать неиспользуемые'", "ru");
	Иначе
		Заголовок = НСтр("ru='Скрыть неиспользуемые'", "ru");
	КонецЕсли;
	
	Команды.Найти("СкрытьНеиспользуемые").Заголовок = Заголовок;
	
КонецПроцедуры

#КонецОбласти