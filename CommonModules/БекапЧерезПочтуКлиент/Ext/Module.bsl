#Область ПрограммныйИнтерфейс

Процедура ВопросВыгрузкиЕмейлБекапаПриНачалеРаботыСистемыЗавершение(Результат, ДопПараметры) Экспорт 
	
	Если Результат = КодВозвратаДиалога.Нет Тогда 
		Возврат;
	КонецЕсли;
	
	ВыгрузитьБекапНаПочту(ДопПараметры);
	
КонецПроцедуры

Процедура ЗадатьВопросВыгрузкиБекапа() Экспорт 
	
	ТекстОтносительнойДатыБекапа = СтандартныеПодсистемыКлиент.ПараметрКлиента("ТекстОтносительнойДатыБекапа");
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ВопросВыгрузкиЕмейлБекапаПриНачалеРаботыСистемыЗавершение", БекапЧерезПочтуКлиент, 
	Новый Структура("ОбработкаПродолжения")),
	СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Последний e-mail бекап: %1. Выгрузить?'"),
	ТекстОтносительнойДатыБекапа), РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

Процедура ВыгрузитьБекапНаПочту(ДопПараметры) Экспорт 
	
	ОП = Новый ОписаниеОповещения("ПослеВыгрузкиБекапа", БекапЧерезПочтуКлиент);
	
	ОткрытьФорму("Обработка.Обмены.Форма.Форма", Новый Структура("ВыгрузкаБекапаНаПочту", Истина), ДопПараметры, , , , ОП,
	РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ПослеВыгрузкиБекапа(Результат, ДопПараметры) Экспорт 
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиентаПриЗапуске();
	
	ЗавершитьРаботуСистемы();
	
КонецПроцедуры

Процедура ПередЗавершениемРаботыСистемы(Отказ, Предупреждения) Экспорт 
	
	НеобходимаВыгрузкаБекапаНаПочту = СтандартныеПодсистемыКлиент.ПараметрКлиента("НеобходимаВыгрузкаБекапаНаПочту");
	
	Если Не НеобходимаВыгрузкаБекапаНаПочту Тогда 
		Возврат;
	КонецЕсли;
	
	ТекстОтносительнойДатыБекапа = СтандартныеПодсистемыКлиент.ПараметрКлиента("ТекстОтносительнойДатыБекапа");
	
	ПредупреждениеПользователю = СтандартныеПодсистемыКлиент.ПредупреждениеПриЗавершенииРаботы();
	ПредупреждениеПользователю.ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Последний e-mail бекап: %1'"),
	ТекстОтносительнойДатыБекапа);
	ПредупреждениеПользователю.ДействиеПриНажатииГиперссылки = "БекапЧерезПочтуКлиент.ЗадатьВопросВыгрузкиБекапа()";
	
	Предупреждения.Добавить(ПредупреждениеПользователю);
	
КонецПроцедуры

#КонецОбласти