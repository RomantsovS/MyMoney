#Область ПрограммныйИнтерфейс

Функция ПолучениеСМССообщения(МассивТелефонов, Период = Неопределено) Экспорт 
	
	#Если МобильноеПриложениеКлиент Тогда 		
		ПоддерживаетсяЖурналСМС = СредстваТелефонии.ПоддерживаетсяЖурналSMS();
		
		Если ПоддерживаетсяЖурналСМС Тогда 
			Попытка
				ЖурналСМС = СредстваТелефонии.ПолучитьЖурналSMS();
				
				Отбор = Новый ОтборКомпоновкиДанных;
				ГруппаОтбора = Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
				ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
				ГруппаОтбора.Использование = Истина;
				
				ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаПолучения");
				ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
				ЭлементОтбора.ПравоеЗначение = Период.ДатаНачала;
				ЭлементОтбора.Использование = Истина;
				
				ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаПолучения");
				ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
				ЭлементОтбора.ПравоеЗначение = Период.ДатаОкончания;
				ЭлементОтбора.Использование = Истина;
				
				ЭлементОтбора = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТипСообщения");
				ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
				ЭлементОтбора.ПравоеЗначение = ТипSMSСредствТелефонии.Входящее;
				ЭлементОтбора.Использование = Истина;
				
				Если МассивТелефонов.Количество() > 0 Тогда 
					ЭлементОтбора = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
					ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НомераТелефонов");
					ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
					ЭлементОтбора.ПравоеЗначение = МассивТелефонов;
					ЭлементОтбора.Использование = Истина;
				КонецЕсли;
				
				МассивСМС = ЖурналСМС.НайтиЗаписи(Отбор);
				
				Возврат МассивСМС;
			Исключение
				Сообщить(ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;		
	#Иначе
		Мас = Новый Массив;
		
		Структ = Новый Структура("ДатаПолучения, НомераТелефонов, Текст");	
		Структ.ДатаПолучения = ТекущаяДата();	
		МасНом = Новый Массив;
		МасНом.Добавить("Tinkoff");	
		Структ.НомераТелефонов = МасНом;	
		Структ.Текст = "Покупка. Карта *0754. 384 RUB. MYASNOV. Доступно 32678.93 RUB";	
		Мас.Добавить(Структ);	
		
		Структ = Новый Структура("ДатаПолучения, НомераТелефонов, Текст");
		Структ.ДатаПолучения = Дата(2018, 6, 5, 1, 1, 1);	
		МасНом = Новый Массив;
		МасНом.Добавить("900");	
		Структ.НомераТелефонов = МасНом;	
		Структ.Текст = "Перевод 4960р от Андрей Михайлович К. Баланс ECMC6285: 15000р";
		Мас.Добавить(Структ);
		
		Структ = Новый Структура("ДатаПолучения, НомераТелефонов, Текст");
		Структ.ДатаПолучения = ТекущаяДата();	
		МасНом = Новый Массив;
		МасНом.Добавить("900");	
		Структ.НомераТелефонов = МасНом;	
		Структ.Текст = "ECMC6285 05.06.18 12:44 Оплата 1992.60р Баланс 31899.56р";	
		Мас.Добавить(Структ);
		
		Возврат Мас;
	#КонецЕсли
	
КонецФункции

Функция ПоддерживаетсяЖурналСМС() Экспорт 
	
	#Если МобильноеПриложениеКлиент Тогда 
		
		Попытка
			Возврат СредстваТелефонии.ПоддерживаетсяЖурналSMS() И СредстваТелефонии.ПоддерживаетсяЖурналЗвонков();
		Исключение
			Сообщить(НСтр("ru = 'Возникла ошибка при определении возможности поддержки журнала СМС
			|Возможно, для приложения не включено разрешение на СМС и Телефон'"));
		КонецПопытки;
		
	#КонецЕсли
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти