#Область ПрограммныйИнтерфейс

Процедура ВопросВыгрузкиРезервнойКопииНаПочтуЗавершение(Результат, ДопПараметры) Экспорт 
	
	Если Результат = КодВозвратаДиалога.Нет Тогда 
		Возврат;
	КонецЕсли;
	
	ВыгрузитьРезервнуюКопиюНаПочту(ДопПараметры);
	
КонецПроцедуры

Процедура ЗадатьВопросВыгрузкиРезервнойКопии() Экспорт 
	
	ТекстОтносительнойДатыРезервнойКопии = СтандартныеПодсистемыКлиент.ПараметрКлиента("ТекстОтносительнойДатыРезервнойКопии");
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ВопросВыгрузкиРезервнойКопииНаПочтуЗавершение", РезервноеКопированиеЧерезПочтуКлиент, 
	Новый Структура("ОбработкаПродолжения")),
	СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Последняя резервная копия e-mail: %1. Выгрузить?'"),
	ТекстОтносительнойДатыРезервнойКопии), РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

Процедура ВыгрузитьРезервнуюКопиюНаПочту(ДопПараметры) Экспорт 
	
	ОП = Новый ОписаниеОповещения("ПослеВыгрузкиРезервнойКопии", РезервноеКопированиеЧерезПочтуКлиент);
	
	ОткрытьФорму("Обработка.Обмены.Форма.Форма", Новый Структура("ВыгрузкаРезервнойКопииНаПочту", Истина), ДопПараметры, , , , ОП,
	РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ПослеВыгрузкиРезервнойКопии(Результат, ДопПараметры) Экспорт 
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиентаПриЗапуске();
	
	ЗавершитьРаботуСистемы();
	
КонецПроцедуры

Процедура ПередЗавершениемРаботыСистемы(Отказ, Предупреждения) Экспорт 
	
	НеобходимаВыгрузкаРезервнойКопииНаПочту = СтандартныеПодсистемыКлиент.ПараметрКлиента("НеобходимаВыгрузкаРезервнойКопииНаПочту");
	
	Если Не НеобходимаВыгрузкаРезервнойКопииНаПочту Тогда 
		Возврат;
	КонецЕсли;
	
	ТекстОтносительнойДатыРезервнойКопии = СтандартныеПодсистемыКлиент.ПараметрКлиента("ТекстОтносительнойДатыРезервнойКопии");
	
	ПредупреждениеПользователю = СтандартныеПодсистемыКлиент.ПредупреждениеПриЗавершенииРаботы();
	ПредупреждениеПользователю.ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Последняя резервная копия e-mail: %1'"),
	ТекстОтносительнойДатыРезервнойКопии);
	ПредупреждениеПользователю.ДействиеПриНажатииГиперссылки = "РезервноеКопированиеЧерезПочтуКлиент.ЗадатьВопросВыгрузкиРезервнойКопии()";
	
	Предупреждения.Добавить(ПредупреждениеПользователю);
	
КонецПроцедуры

#КонецОбласти