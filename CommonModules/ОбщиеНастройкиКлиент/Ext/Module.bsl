Процедура ВыбратьНастройку(Форма) Экспорт 
	
	//ОткрытьФорму("Справочник.КлючНастроек.Форма.ФормаСписка", Новый Структура("КлючОбъекта", Форма.КлючОбъекта), Форма,,,,
	//Новый ОписаниеОповещения("ВыбратьНастройкуЗавершение", ОбщиеНастройкиКлиент, Новый Структура("Форма", Форма)),
	//РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	ОткрытьФорму("РегистрСведений.ОбщиеНастройки.ФормаСписка", Новый Структура("РежимВыбора, КлючОбъекта", Истина, Форма.КлючОбъекта),
	Форма,,,, Новый ОписаниеОповещения("ВыбратьНастройкуЗавершение", ОбщиеНастройкиКлиент, Новый Структура("Форма", Форма)),
	РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ВыбратьНастройкуЗавершение(Результат, ДопПараметры) Экспорт 
	Если Результат <> Неопределено Тогда 
		Форма = ДопПараметры.Форма;
		
		Форма.ТекНастройка = Результат;
		
		Форма.ВыбратьНастройкуЗавершениеСервер();
		
		Форма.ОбновитьТекущуюСтраницу();
	КонецЕсли;
КонецПроцедуры

Процедура СохранитьНастройку(Форма) Экспорт 
	
	Если ЗначениеЗаполнено(Форма.ТекНастройка) Тогда 
		Спис = Новый СписокЗначений;
		
		Если ОбщиеНастройкиВызовСервера.СохранитьНастройкуНаСервереПроверкаТекНастройки(Форма.ТекНастройка) Тогда 
			Спис.Добавить(Форма.ТекНастройка, "Перезаписать """ + Форма.ТекНастройка + """");
		КонецЕсли;
		
		Спис.Добавить(ПредопределенноеЗначение("Справочник.КлючНастроек.ПустаяСсылка"), НСтр("ru = 'Создать новую'"));
		
		ОткрытьФорму("ОбщаяФорма.ФормаВыбораИзСписка", Новый Структура("Список", Спис), Форма, , , ,
		Новый ОписаниеОповещения("СохранитьНастройкуВыборИзСпискаЗавершение", ОбщиеНастройкиКлиент, Новый Структура("Форма", Форма)),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		СохранитьНастройкуВыборИзСпискаЗавершение(ПредопределенноеЗначение("Справочник.КлючНастроек.ПустаяСсылка"), Новый Структура("Форма", Форма))
	КонецЕсли;	
	
КонецПроцедуры

Процедура СохранитьНастройкуВыборИзСпискаЗавершение(РезультатВыбора, ДопПараметры) Экспорт 
	Если РезультатВыбора = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ИмяНастройки = "";
	
	Если ТипЗнч(РезультатВыбора) = Тип("СправочникСсылка.КлючНастроек") Тогда 
		ИмяНастройки = Строка(РезультатВыбора);
	КонецЕсли;
	
	ПоказатьВводСтроки(Новый ОписаниеОповещения("СохранитьНастройкуВводСтрокиЗавершение", ОбщиеНастройкиКлиент,
	Новый Структура("Форма, КлючНастроек", ДопПараметры.Форма, РезультатВыбора)),
	ИмяНастройки, НСтр("ru = 'Введите имя настройки'"), 50);
КонецПроцедуры

Процедура СохранитьНастройкуВводСтрокиЗавершение(ИмяНастройки, ДопПараметры) Экспорт 
	
	Если ИмяНастройки = "" Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Необходимо ввести имя настройки!'"));
		Возврат;
	КонецЕсли;
	
	Если ОбщиеНастройкиВызовСервера.ЕстьКлючНастроекПоНаименованиюСервер(ИмяНастройки, ДопПараметры.КлючНастроек) Тогда 
		ПоказатьПредупреждение(, НСтр("ru = 'Настройка с таким именем уже существует!'"));
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитОбъекта(ДопПараметры.Форма, "АдресНастроекСКД") Тогда 
		ТекНастройка = ОбщиеНастройкиВызовСервера.СохранитьНастройкуНаСервере(ДопПараметры.Форма.КлючОбъекта, ДопПараметры.КлючНастроек,
		ИмяНастройки, ДопПараметры.Форма.АдресНастроекСКД);
		
		ДопПараметры.Форма.ТекНастройка = ТекНастройка;
	Иначе
		ДопПараметры.Форма.СохранитьНастройкуНаСервере(ДопПараметры.КлючНастроек, ИмяНастройки);	
	КонецЕсли;
	
КонецПроцедуры

Процедура НастройкаПорядка(Форма, Параметры) Экспорт 	
	
	ОП = Новый ОписаниеОповещения("НастройкаПорядкаПослеИзменения", ОбщиеНастройкиКлиент, Новый Структура("Форма", Форма));
	
	ОткрытьФорму("Обработка.Отчеты.Форма.НастройкиПорядка", Параметры, , , , , ОП);
	
КонецПроцедуры

Процедура НастройкаПорядкаПослеИзменения(Результат, ДопПараметры) Экспорт 
	
	Если Результат <> Неопределено Тогда 
		ДопПараметры.Форма.НастройкаПорядкаПослеИзмененияСерв(Результат, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

Процедура НастройкаОтбора(Форма, Параметры) Экспорт 
	
	ОП = Новый ОписаниеОповещения("НастройкаОтбораПослеИзменения", ОбщиеНастройкиКлиент, Новый Структура("Форма", Форма));
	
	ОткрытьФорму("Обработка.Отчеты.Форма.НастройкиОтбора", Параметры, , , , , ОП);
	
КонецПроцедуры

Процедура НастройкаОтбораПослеИзменения(Настройка, ДопПараметры) Экспорт 
	
	Если Настройка <> Неопределено Тогда
		ДопПараметры.Форма.НастройкаОтбораПослеИзменения(Настройка, ДопПараметры);
	КонецЕсли;
	
КонецПроцедуры