////////////////////////////////////////////////////////////////////////////////
//Обработка.НастройкаПрограммы.Формы.ФормаИзмененияПароля
//  Предназначена Для назначения Или изменения пароля на вход в программу
//  
//Параметры формы:
//  Стандартные параметры формы
//  
////////////////////////////////////////////////////////////////////////////////


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДопРеквизитыФормы = Новый Структура;
	ДопРеквизитыФормы.Вставить("ЦветТекстаОшибки", ЦветаСтиля.ЦветОтрицательногоЧисла);
	ДопРеквизитыФормы.Вставить("ЦветТекстаФормы", ЦветаСтиля.ЦветТекстаФормы);
	ДопРеквизитыФормы.Вставить("СтарыйПарольУказанПравильно", Параметры.Свойство("СтарыйПарольУказанПравильно"));
	
	ОтображениеПинКода1 = "○○○○";
	ОтображениеПинКода2 = "○○○○";
	
	СтарыйПарольПуст = НЕ ЗначениеЗаполнено(НаборКонстант.ПарольДляВхода);
	ЗаполняемыйПинКод = 1;
	
	Если СтарыйПарольПуст Или 
			(СтрДлина(НаборКонстант.ПарольДляВхода) = 4 И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(НаборКонстант.ПарольДляВхода)) Тогда
		СпособЗащиты = 1;
		Элементы.ГруппаСтраницыПароля.ТекущаяСтраница = Элементы.СтраницаПинКод;
	Иначе
		СпособЗащиты = 2;
		Элементы.ГруппаСтраницыПароля.ТекущаяСтраница = Элементы.СтраницаПароль;
	КонецЕсли;
	
	Если СтарыйПарольПуст Тогда
		Заголовок = НСтр("ru = 'Установка защиты на вход'");
		Элементы.НовыйПароль.Заголовок = НСтр("ru = 'Введите пароль'");
		Элементы.ПодтверждениеНовогоПароля.Заголовок = НСтр("ru = 'Введите пароль еще раз'");
		Элементы.ИзменитьПароль.Заголовок = НСтр("ru = '  Установить  '");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособЗащитыПриИзменении(Элемент)
	
	Если СпособЗащиты = 1 Тогда
		Элементы.ГруппаСтраницыПароля.ТекущаяСтраница = Элементы.СтраницаПинКод;
	Иначе
		Элементы.ГруппаСтраницыПароля.ТекущаяСтраница = Элементы.СтраницаПароль;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура Кнопка1Нажатие(Элемент)
	
	ИзменитьПинКод(1);
	
КонецПроцедуры

&НаКлиенте
Процедура Кнопка2Нажатие(Элемент)
	
	ИзменитьПинКод(2);
	
КонецПроцедуры

&НаКлиенте
Процедура Кнопка3Нажатие(Элемент)
	
	ИзменитьПинКод(3);
	
КонецПроцедуры

&НаКлиенте
Процедура Кнопка4Нажатие(Элемент)
	
	ИзменитьПинКод(4);
	
КонецПроцедуры

&НаКлиенте
Процедура Кнопка5Нажатие(Элемент)
	
	ИзменитьПинКод(5);
	
КонецПроцедуры

&НаКлиенте
Процедура Кнопка6Нажатие(Элемент)
	
	ИзменитьПинКод(6);
	
КонецПроцедуры

&НаКлиенте
Процедура Кнопка7Нажатие(Элемент)
	
	ИзменитьПинКод(7);
	
КонецПроцедуры

&НаКлиенте
Процедура Кнопка8Нажатие(Элемент)
	
	ИзменитьПинКод(8);
	
КонецПроцедуры

&НаКлиенте
Процедура Кнопка9Нажатие(Элемент)
	
	ИзменитьПинКод(9);
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаОтменаНажатие(Элемент)
	
	Закрыть(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Кнопка0Нажатие(Элемент)
	
	ИзменитьПинКод(0);
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаУдалитьНажатие(Элемент)
	
	Если ЗаполняемыйПинКод = 1 Тогда
		ДлинаВводимыйПинКод = СтрДлина(НовыйПароль);
		НовыйПароль         = Лев(НовыйПароль, ДлинаВводимыйПинКод - 1);
	Иначе
		ДлинаВводимыйПинКод       = СтрДлина(ПодтверждениеНовогоПароля);
		ПодтверждениеНовогоПароля = Лев(ПодтверждениеНовогоПароля, ДлинаВводимыйПинКод - 1);
	КонецЕсли;
	
	ПроверитьПинКод();
	
КонецПроцедуры



#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьПароль(Команда)
	
	// Проверим совпадение полей нового пароля
	НовыйПароль = СокрЛП(НовыйПароль);
	ПодтверждениеНовогоПароля = СокрЛП(ПодтверждениеНовогоПароля);
	Если НовыйПароль <> ПодтверждениеНовогоПароля Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Новый пароль и подтверждение пароля не совпадают.'"));
		НовыйПароль = "";
		ПодтверждениеНовогоПароля = "";
		ТекущийЭлемент = Элементы.НовыйПароль;
		Возврат;
	КонецЕсли;
	
	// Если Новый пароль пуст
	Если НЕ ЗначениеЗаполнено(НовыйПароль) И НЕ ЗначениеЗаполнено(ПодтверждениеНовогоПароля) Тогда
		ТекстВопроса = НСтр("ru = 'Новый пароль пуст.
								  |Удалить пароль Для входа?'");
		ПоказатьВопрос(Новый ОписаниеОповещения("ИзменитьПарольВопросЗавершение", ЭтотОбъект), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзменитьПарольВопросЗавершение(Результат, ДопПараметры) Экспорт 
	
	Если Результат = КодВозвратаДиалога.Нет Тогда 
		Возврат;
	КонецЕсли;
	
	НаборКонстант.ПарольДляВхода = НовыйПароль;
	Записать();
	Закрыть(КодВозвратаДиалога.ОК);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПинКод()
	
	ОбновитьОтображениеПинКода();
	ДлинаПароля1 = СтрДлина(НовыйПароль);
	ДлинаПароля2 = СтрДлина(ПодтверждениеНовогоПароля);
	
	Если ЗаполняемыйПинКод = 2 И ДлинаПароля2 = 4 Тогда
		
		Если НовыйПароль = ПодтверждениеНовогоПароля Тогда
			
			НаборКонстант.ПарольДляВхода = НовыйПароль;
			Записать();
			Закрыть(КодВозвратаДиалога.ОК);			
		Иначе
			
			Элементы.ДекорацияСообщение.Заголовок = НСтр("ru='Пин-коды не совпадают'");
			Элементы.ДекорацияСообщение.ЦветТекста = ДопРеквизитыФормы.ЦветТекстаОшибки;
			Элементы.ГруппаКнопок.Доступность = Ложь;
			ПодключитьОбработчикОжидания("Подключаемый_ОчиститьПинКод", 0.5, Истина);
			
		КонецЕсли;
		
	ИначеЕсли ЗаполняемыйПинКод = 1 И ДлинаПароля1 = 4 Тогда
		
		ЗаполняемыйПинКод = 2;
		Элементы.ДекорацияСообщение.Заголовок = НСтр("ru='Повторите пин-код:'");
		
	КонецЕсли;
	 
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеПинКода()
	
	ОтображениеПинКода = "○○○○";
	ДлинаПароля = ?(ЗаполняемыйПинКод = 1, СтрДлина(НовыйПароль), СтрДлина(ПодтверждениеНовогоПароля));
	
	Если ДлинаПароля = 0 Тогда
		ОтображениеПинКода = "○○○○";
	ИначеЕсли ДлинаПароля = 1 Тогда
		ОтображениеПинКода = "●○○○";
	ИначеЕсли ДлинаПароля = 2 Тогда
		ОтображениеПинКода = "●●○○";
	ИначеЕсли ДлинаПароля = 3 Тогда
		ОтображениеПинКода = "●●●○";
	ИначеЕсли ДлинаПароля = 4 Тогда
		ОтображениеПинКода = "●●●●";
	КонецЕсли;
	
	Если ЗаполняемыйПинКод = 1 Тогда
		ОтображениеПинКода1 = ОтображениеПинКода;
	Иначе
		ОтображениеПинКода2 = ОтображениеПинКода;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОчиститьПинКод()
	
	ОтображениеПинКода1 = "○○○○";
	ОтображениеПинКода2 = "○○○○";
	
	ПодтверждениеНовогоПароля = "";
	
	Элементы.ГруппаКнопок.Доступность = Истина;
	Пароль = "";
	Элементы.ДекорацияСообщение.Заголовок = НСтр("ru='Введите пин-код:'");
	Элементы.ДекорацияСообщение.ЦветТекста = ДопРеквизитыФормы.ЦветТекстаФормы;
	ОбновитьОтображениеПинКода();
	ЗаполняемыйПинКод = 1;
	НовыйПароль = "";
	ОбновитьОтображениеПинКода();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьПинКод(Цифра)

	Если ЗаполняемыйПинКод = 1 Тогда
		НовыйПароль = НовыйПароль + Строка(Цифра);
	Иначе
		ПодтверждениеНовогоПароля = ПодтверждениеНовогоПароля + Строка(Цифра);
	КонецЕсли;
	
	ПроверитьПинКод();

КонецПроцедуры

#КонецОбласти

