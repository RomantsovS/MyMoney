#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОтчетыСКДВызовСервера.ПриСозданииНаСервере(ЭтотОбъект);
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда 
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДиаграммы И ТекУровеньГруппировки > 0 Тогда 
		Отказ = Истина;
		СтандартнаяОбработка = Ложь;
		
		ПередЗакрытиемНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда 
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПриЗакрытииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТабДокОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ОтборПоТипуОперации", ПредопределенноеЗначение("Перечисление.ТипыОпераций.Перевод"));
	
	КомпоновкаДанныхКлиент.ОбработкаРасшифровки(ЭтотОбъект, Элемент, Расшифровка, АдресДанныхРасшифровкиОтчет, СтандартнаяОбработка, ДопПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура Группа2ПриСменеСтраницы(Элемент, ТекущаяСтраница)
	ОбновитьТекущуюСтраницу();
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаРасходыВыбор(Элемент, ЗначениеДиаграммы, СтандартнаяОбработка)	
	
	КомпоновкаДанныхКлиент.ДиаграммаВыбор(ЭтотОбъект, Элемент, ЗначениеДиаграммы, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьДиаграмму(Команда)
	ОбновитьТекущуюСтраницу();
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗначенияДиаграммы(Команда)
	ПроверитьЗначенияДиаграммыНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура НазадУровеньДиаграммыКоманда(Команда)
	
	Если ТекУровеньГруппировки = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	НазадУровеньДиаграммы();
	
КонецПроцедуры

&НаСервере
Процедура НазадУровеньДиаграммы()
	
	ОтчетыСКДВызовСервера.НазадУровеньДиаграммы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьОформлениеДиаграммы()
	ШрифтПодписей = Новый Шрифт(ШрифтыСтиля.МелкийШрифтТекста);
	
	ДиаграммаРеквизит.ШрифтПодписей = ШрифтПодписей;
	
	ДиаграммаРеквизит.ОбластьПостроения.Шрифт = ШрифтПодписей;
	ДиаграммаРеквизит.ОбластьПостроения.Верх = 0;
	ДиаграммаРеквизит.ОбластьПостроения.Лево = 0;
	ДиаграммаРеквизит.ОбластьПостроения.Низ = 1;
	ДиаграммаРеквизит.ОбластьПостроения.Право = 1;
	
	ДиаграммаРеквизит.ОбластьПостроения.ЦветШкалы = WebЦвета.Черный;
	
	ДиаграммаРеквизит.ФорматЗначенийВПодписях  = "ЧДЦ=2; ЧРГ=.";
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	КлючОбъекта = Метаданные.Обработки.Отчеты.Имя + ИмяФормы;
	
	ТекУровеньГруппировки = 0;
	
	ОбщиеНастройкиВызовСервера.ЗагрузитьОсновнуюНастройку(ЭтотОбъект);
	
	НастроитьОформлениеДиаграммы();
	
	ОбновитьТекущуюСтраницу();
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкуДоп(Настройки) Экспорт 
	
	ОтборТипОперации.Добавить(Перечисления.ТипыОпераций.Перевод);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТекущуюСтраницу() Экспорт 
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДиаграммы Тогда
		Если Не ДиаграммаСформирована Тогда 
			ОбновитьДиаграммуСервер();
		КонецЕсли;
	Иначе
		Если Не ОтчетСформирован Тогда 
			ОбновитьСерверСКД();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСерверСКД()
	
	ТабДок.Очистить();
	
	СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(ЭтотОбъект.АдресСКД);
	
	ОтчетыСКДВызовСервера.ПолучитьДанныеОтчета(СхемаКомпоновкиДанных, ТабДок, Неопределено, УникальныйИдентификатор, Неопределено,
	АдресДанныхРасшифровкиОтчет, Истина, ЭтотОбъект.АдресНастроекСКД);
	
	ОтчетСформирован = Истина;
	
	#Если МобильноеПриложениеСервер Тогда
	#Иначе		
		Элементы.ТабДок.ОтображатьГруппировки = Истина;
	#КонецЕсли	
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДиаграммуСервер() Экспорт 
	
	СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(ЭтотОбъект.АдресСКД);
	
	ВыбранныеПоля = ОтчетыСКДВызовСервера.ПустаяТаблицаВыбранныеПоля();
	
	СтруктураКомпоновки = ОтчетыСКДВызовСервера.ПустаяТаблицаСтруктураКомпоновки();
	
	ОтчетыСКДВызовСервера.ПолучитьПараметрыНастроекСКД(СхемаКомпоновкиДанных, ЭтотОбъект.АдресНастроекСКД, СтруктураКомпоновки, ВыбранныеПоля);
	
	ДиаграммаРеквизит.Очистить();
	ДиаграммаРеквизит.Обновление = Ложь;
	
	ПорядокИтоговОбработанный = СтруктураКомпоновки.НайтиСтроки(Новый Структура("Использование", Истина));
	
	ВыбранныеПоляОбработанные = ВыбранныеПоля.НайтиСтроки(Новый Структура("Использование", Истина));
	
	МаксУровеньГруппировки = ПорядокИтоговОбработанный.Количество();
	
	Если ПорядокИтоговОбработанный.Количество() > 0 И ВыбранныеПоляОбработанные.Количество() > 0 Тогда			
		ПорядокИтоговНов = Новый ТаблицаЗначений;
		ПорядокИтоговНов.Колонки.Добавить("Поле");
		ПорядокИтоговНов.Колонки.Добавить("Использование");
		
		СтрПолеИтог = ПорядокИтоговОбработанный[ТекУровеньГруппировки];
			
		НоваяСтрока = ПорядокИтоговНов.Добавить();
		НоваяСтрока.Поле = СтрПолеИтог.Поле;		
		НоваяСтрока.Использование = Истина;
		
		СтруктураПараметров = ПолучитьСтруктуруПараметров();
		
		ДанныеОтчета = Новый ДеревоЗначений;
		ОтчетыСКДВызовСервера.ПолучитьДанныеОтчета(СхемаКомпоновкиДанных, ДанныеОтчета, ПорядокИтоговНов, УникальныйИдентификатор,
		СтруктураПараметров, АдресДанныхРасшифровкиДиаграмма, Ложь, ЭтотОбъект.АдресНастроекСКД);
		
		ТекЗаголовок = "";
		
		Для Индекс = 0 По ТекУровеньГруппировки - 1 Цикл
			Если ПорядокИтоговОбработанный[Индекс].Поле = "Валюта" Тогда 
				ТекЗаголовок = ТекЗаголовок + "/" + ОтборДопВалюта[0].Значение;
			ИначеЕсли ПорядокИтоговОбработанный[Индекс].Поле = "Кошелек" Тогда 
				ТекЗаголовок = ТекЗаголовок + "/" + ОтборДопКошелек[0].Значение;
			ИначеЕсли ПорядокИтоговОбработанный[Индекс].Поле = "Метка" Тогда 
				ТекЗаголовок = ТекЗаголовок + "/" + ОтборДопМетка[0].Значение;
			ИначеЕсли ПорядокИтоговОбработанный[Индекс].Поле = "Статья" Тогда 
				ТекЗаголовок = ТекЗаголовок + "/" + ОтборДопСтатья[0].Значение;
			ИначеЕсли ПорядокИтоговОбработанный[Индекс].Поле = "ТипОперации" Тогда 
				ТекЗаголовок = ТекЗаголовок + "/" + ОтборДопТипОперации[0].Значение;
			КонецЕсли;
		КонецЦикла;
		
		Элементы.Декорация1.Заголовок = Заголовок + ТекЗаголовок + "/[" + СтрПолеИтог.Поле + "]";
		
		Точка = ДиаграммаРеквизит.Точки.Добавить();
		
		Для Каждого Стр из ДанныеОтчета.Строки Цикл
			Серия = ДиаграммаРеквизит.Серии.Добавить(Стр[СтрПолеИтог.Поле]);
			Серия.Значение = Стр[СтрПолеИтог.Поле];
			Серия.Текст = Стр[СтрПолеИтог.Поле];
			
			Расшифровка = Строка(Серия.Значение) + " - " + Формат(Стр.Сумма, "ЧДЦ=2; ЧРГ=.; ЧН=0.00");
			
			ДиаграммаРеквизит.УстановитьЗначение(Точка, Серия, Стр.Сумма, Расшифровка, Расшифровка);
		КонецЦикла;
		
		ДиаграммаРеквизит.Обновление = Истина;
		
		Элементы.СтраницыДиаграмм.ТекущаяСтраница = Элементы.СтраницаОстатки;		
	Иначе
		Элементы.СтраницыДиаграмм.ТекущаяСтраница = Элементы.СтраницаНетДанных;
	КонецЕсли;
	
	ДиаграммаСформирована = Истина;
КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруПараметров()
	
	СтруктураПараметров = Новый Структура;
	//СтруктураПараметров.Вставить("Период", Период);
	//СтруктураПараметров.Вставить("ОтборВалюта", ОтборВалюта);
	СтруктураПараметров.Вставить("ОтборДопВалюта", ОтборДопВалюта);
	//СтруктураПараметров.Вставить("ОтборКошелек", ОтборКошелек);
	СтруктураПараметров.Вставить("ОтборДопКошелек", ОтборДопКошелек);
	//СтруктураПараметров.Вставить("ОтборМетка", ОтборМетка);
	СтруктураПараметров.Вставить("ОтборДопМетка", ОтборДопМетка);
	//СтруктураПараметров.Вставить("ОтборСтатья", ОтборСтатья);
	СтруктураПараметров.Вставить("ОтборДопСтатья", ОтборДопСтатья);
	//СтруктураПараметров.Вставить("ОтборТипОперации", ОтборТипОперации);
	СтруктураПараметров.Вставить("ОтборДопТипОперации", ОтборДопТипОперации);
	
	Возврат СтруктураПараметров;
	
КонецФункции

&НаСервере
Процедура ВыбратьНастройкуЗавершениеСервер() Экспорт 
	
	ОбщиеНастройкиВызовСервера.ВыбратьНастройкуЗавершениеСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьЗначенияДиаграммыНаСервере()
	Для Каждого стрТочка из ДиаграммаРеквизит.Точки Цикл
		Для Каждого стрСерия из ДиаграммаРеквизит.Серии Цикл
			значДиаграммы = ДиаграммаРеквизит.ПолучитьЗначение(стрТочка, стрСерия);
			
			Сообщить("" + значДиаграммы.Серия.текст + " " + значДиаграммы.точка.текст + " " + значДиаграммы.Значение);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПередЗакрытиемНаСервере()
	
	ОтчетыСКДВызовСервера.НазадУровеньДиаграммы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	ОбщиеНастройкиВызовСервера.СохранитьНастройкуОтчетаПриЗакрытии(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти