#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	
	ОтчетыСКД.ПриСозданииНаСервере(ЭтотОбъект);
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда 
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДиаграммы И ТекУровеньГруппировки > 0 Тогда 
		Отказ = Истина;
		СтандартнаяОбработка = Ложь;
		
		ПередЗакрытиемНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда 
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПриЗакрытииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТабДокОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	КомпоновкаДанныхКлиент.ОбработкаРасшифровки(ЭтотОбъект, Элемент, Расшифровка, АдресДанныхРасшифровкиОтчет, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Группа2ПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Группа2ПриСменеСтраницыСерв();	
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаОстаткиВыбор(Элемент, ЗначениеДиаграммы, СтандартнаяОбработка)
	
	КомпоновкаДанныхКлиент.ДиаграммаВыбор(ЭтотОбъект, Элемент, ЗначениеДиаграммы, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьОформлениеДиаграммы()
	
	ШрифтПодписей = Новый Шрифт(ШрифтыСтиля.МелкийШрифтТекста);
	
	ДиаграммаРеквизит.ШрифтПодписей = ШрифтПодписей;
	
	ДиаграммаРеквизит.ОбластьПостроения.Шрифт = ШрифтПодписей;
	ДиаграммаРеквизит.ОбластьПостроения.Верх = 0;
	ДиаграммаРеквизит.ОбластьПостроения.Лево = 0;
	ДиаграммаРеквизит.ОбластьПостроения.Низ = 1;
	ДиаграммаРеквизит.ОбластьПостроения.Право = 1;
	
	ДиаграммаРеквизит.ОбластьПостроения.ЦветШкалы = WebЦвета.Черный;
	
	ДиаграммаРеквизит.ФорматЗначенийВПодписях  = "ЧДЦ=2; ЧРГ=.";
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	//новСтр = ПорядокИтогов.Добавить();
	//новСтр.поле = "Валюта";
	//новСтр.использовать = Истина;
	//новСтр = ПорядокИтогов.Добавить();
	//новСтр.поле = "Кошелек";
	//новСтр.использовать = Истина;
	
	КлючОбъекта = Метаданные.Обработки.Отчеты.Имя + ЭтаФорма.ИмяФормы;
	
	ТекУровеньГруппировки = 0;
	
	ОбщиеНастройкиВызовСервера.ЗагрузитьОсновнуюНастройку(ЭтотОбъект);
	
	НастроитьОформлениеДиаграммы();
	
	ОбновитьТекущуюСтраницу();
	
КонецПроцедуры

&НаСервере
Процедура Группа2ПриСменеСтраницыСерв()
	ОбновитьТекущуюСтраницу();	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТекущуюСтраницу()
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДиаграммы Тогда
		Если Не ДиаграммаСформирована Тогда 
			ОбновитьДиаграммуСервер();			
		КонецЕсли;
	Иначе
		Если Не ОтчетСформирован Тогда 
			ОбновитьСерверСКД();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСерверСКД()
	
	ТабДок.Очистить();
	
	СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(ЭтотОбъект.АдресСКД);
	//СтруктураПараметров = ПолучитьСтруктуруПараметров();
	
	ОтчетыСКД.ПолучитьДанныеОтчета(СхемаКомпоновкиДанных, ТабДок, Неопределено, УникальныйИдентификатор, Неопределено,
	АдресДанныхРасшифровкиОтчет, Истина, ЭтотОбъект.АдресНастроекСКД);
	
	ОтчетСформирован = Истина;
	
	#Если МобильноеПриложениеСервер Тогда
	#Иначе		
		Элементы.ТабДок.ОтображатьГруппировки = Истина;
	#КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДиаграммуСервер() Экспорт 
	
	СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(ЭтотОбъект.АдресСКД);
	
	ВыбранныеПоля = ОтчетыСКД.ПустаяТаблицаВыбранныеПоля();
	
	СтруктураКомпоновки = ОтчетыСКД.ПустаяТаблицаСтруктураКомпоновки();
	
	ОтчетыСКД.ПолучитьПараметрыНастроекСКД(СхемаКомпоновкиДанных, ЭтотОбъект.АдресНастроекСКД, СтруктураКомпоновки, ВыбранныеПоля);
	
	ДиаграммаРеквизит.Очистить();
	ДиаграммаРеквизит.Обновление = Ложь;
	
	ПорядокИтоговОбработанный = СтруктураКомпоновки.НайтиСтроки(Новый Структура("Использование", Истина));
	
	ВыбранныеПоляОбработанные = ВыбранныеПоля.НайтиСтроки(Новый Структура("Использование", Истина));
	
	МаксУровеньГруппировки = ПорядокИтоговОбработанный.Количество();
	
	Если ПорядокИтоговОбработанный.Количество() > 0 И ВыбранныеПоляОбработанные.Количество() > 0 Тогда	
		
		ПорядокИтоговНов = Новый ТаблицаЗначений;
		ПорядокИтоговНов.Колонки.Добавить("Поле");
		ПорядокИтоговНов.Колонки.Добавить("Использование");
		ПорядокИтоговНов.Очистить();
		
		СтрПолеИтог = ПорядокИтоговОбработанный[ТекУровеньГруппировки];
			
		НовСтр = ПорядокИтоговНов.Добавить();
		НовСтр.Поле = СтрПолеИтог.Поле;		
		НовСтр.Использование = Истина;
		
		СтруктураПараметров = ПолучитьСтруктуруПараметров();
		
		ДанныеОтчета = Новый ДеревоЗначений;
		ОтчетыСКД.ПолучитьДанныеОтчета(СхемаКомпоновкиДанных, ДанныеОтчета, ПорядокИтоговНов, УникальныйИдентификатор,
		СтруктураПараметров, АдресДанныхРасшифровкиДиаграмма, Ложь, ЭтотОбъект.АдресНастроекСКД);
		
		ТекЗаголовок = "";
		
		Для Индекс = 0 По ТекУровеньГруппировки - 1 Цикл
			Если ПорядокИтоговОбработанный[Индекс].Поле = "Валюта" Тогда 
				ТекЗаголовок = ТекЗаголовок + "/" + ОтборДопВалюта[0].Значение;
			ИначеЕсли ПорядокИтоговОбработанный[Индекс].Поле = "Кошелек" Тогда 
				ТекЗаголовок = ТекЗаголовок + "/" + ОтборДопКошелек[0].Значение;
			КонецЕсли;
		КонецЦикла;
		
		Элементы.Декорация1.Заголовок = Заголовок + ТекЗаголовок + "/[" + СтрПолеИтог.Поле + "]";
		
		Точка = ДиаграммаРеквизит.Точки.Добавить();
		
		Для Каждого Стр из ДанныеОтчета.Строки Цикл
			Серия = ДиаграммаРеквизит.Серии.Добавить(Стр[СтрПолеИтог.Поле]);
			Серия.Значение = Стр[СтрПолеИтог.Поле];
			Серия.Текст = Стр[СтрПолеИтог.Поле];
			
			ТекЗначение = Стр[ВыбранныеПоляОбработанные[0].Поле];
			
			Расшифровка = Строка(Серия.Значение) + " - " + Формат(ТекЗначение, "ЧДЦ=2; ЧРГ=.; ЧН=0.00");
			
			ДиаграммаРеквизит.УстановитьЗначение(Точка, Серия, ТекЗначение, Расшифровка, Расшифровка);
		КонецЦикла;
		
		ДиаграммаРеквизит.Обновление = Истина;
		
		Элементы.СтраницыДиаграмм.ТекущаяСтраница = Элементы.СтраницаОстатки;		
	Иначе
		Элементы.СтраницыДиаграмм.ТекущаяСтраница = Элементы.СтраницаНетДанных;
	КонецЕсли;
	
	ДиаграммаСформирована = Истина;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСтруктуруПараметров()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ОтборДопВалюта", ОтборДопВалюта);
	СтруктураПараметров.Вставить("ОтборДопКошелек", ОтборДопКошелек);
	
	Возврат СтруктураПараметров;
	
КонецФункции

&НаСервере
Процедура ВыбратьНастройкуЗавершениеСервер() Экспорт 
	
	ОбщиеНастройкиВызовСервера.ВыбратьНастройкуЗавершениеСервер(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗакрытиемНаСервере()
	
	ОтчетыСКД.НазадУровеньДиаграммы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	ОбщиеНастройкиВызовСервера.СохранитьНастройкуОтчетаПриЗакрытии(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти