#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПараметрыЗагрузкиКурсовВалют() Экспорт 
	
	Результат = Новый Структура("НачалоПериодаЗагрузки, ОкончаниеПериодаЗагрузки, Валюты");
	
	ТекущаяДата = ТекущаяДата();
	
	ПоследниеКурсы = РегистрыСведений.КурсыВалют.СрезПоследних();
	
	ПоискРубля = ПоследниеКурсы.НайтиСтроки(Новый Структура("Валюта", ЗначениеНастроекВызовСервераПовтИсп.ПолучитьЗначениеКонстанты("ВалютаУчета")));
	
	Если ПоискРубля.Количество() > 0 Тогда 
		ПоследниеКурсы.Удалить(ПоследниеКурсы.Индекс(ПоискРубля[0]));		
	КонецЕсли;
	
	ПоследниеКурсы.Сортировать("Период");
	
	Если ПоследниеКурсы.Количество() > 0 Тогда 
		Результат.НачалоПериодаЗагрузки = НачалоДня(ПоследниеКурсы[0].Период);
	Иначе
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	МИНИМУМ(Кошельки.Период) КАК Период
		|ИЗ
		|	РегистрНакопления.Кошельки КАК Кошельки");
		Рез = Запрос.Выполнить().Выбрать();
		
		Если Рез.Следующий() Тогда 
			Если Рез.Период <> NULL Тогда 
				Результат.НачалоПериодаЗагрузки = Рез.Период;
			Иначе
				Результат.НачалоПериодаЗагрузки = Мин(НачалоГода(ТекущаяДата), НачалоДня(ТекущаяДата) - 7 * 24 * 60 * 60);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Результат.ОкончаниеПериодаЗагрузки = НачалоДня(ТекущаяДата);
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	Валюты.Ссылка КАК Валюта,
	|	Валюты.Код КАК КодВалюты
	|ИЗ
	|	Справочник.Валюты КАК Валюты
	|ГДЕ
	|	Валюты.Ссылка В(&Валюты)");
	Запрос.УстановитьПараметр("Валюты", ОбщиеНастройкиВызовСервера.ОбщиеНастройкиЗагрузить(Метаданные.Обработки.ЗагрузкаКурсовВалют.Имя,
	ЗагрузитьОсновнуюНастройку(), Новый Массив));
	
	Результат.Валюты = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

Функция ЗагрузитьОсновнуюНастройку() Экспорт 
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	КлючНастроек.Ссылка
	|ИЗ
	|	Справочник.КлючНастроек КАК КлючНастроек
	|ГДЕ
	|	КлючНастроек.Основная
	|	И КлючНастроек.КлючОбъекта = &КлючОбъекта");
	Запрос.УстановитьПараметр("КлючОбъекта", Метаданные.Обработки.ЗагрузкаКурсовВалют.Имя); 
	
	Рез = Запрос.Выполнить().Выбрать();
	
	Если Рез.Следующий() Тогда		
		Возврат Рез.ссылка;
	КонецЕсли;
	
	Возврат Справочники.КлючНастроек.ПустаяСсылка();
	
КонецФункции

#КонецОбласти

#КонецЕсли