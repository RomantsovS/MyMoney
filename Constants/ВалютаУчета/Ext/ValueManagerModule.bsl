
Процедура ПередЗаписью(Отказ)	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;	
	
	ДополнительныеСвойства.Вставить("ПрежнееЗначение", ЭтотОбъект.Значение);
КонецПроцедуры

Процедура ПриЗаписи(Отказ)	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПрежняяВалютаУчета = Неопределено;
	ДополнительныеСвойства.Свойство("ПрежнееЗначение", ПрежняяВалютаУчета);
	
	НоваяВалютаУчета = ЭтотОбъект.Значение;
	
	Если ЗначениеЗаполнено(НоваяВалютаУчета) И ПрежняяВалютаУчета <> НоваяВалютаУчета Тогда
		
		СписокВалютДляПересчета = Новый Массив;
		
		Выборка = Справочники.Валюты.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			// Устанавливаем курс на 01.01.1980 равный 1
			РаботаСКурсамиВалют.ПроверитьКорректностьКурсаНа01_01_1980(Выборка.Ссылка, НоваяВалютаУчета);
			
			// Добавим валюту в список Для пересчета
			Если Выборка.Ссылка <> НоваяВалютаУчета Тогда
				СписокВалютДляПересчета.Добавить(Новый Структура("Валюта,СтатусОперации", Выборка.Ссылка, Истина));
			КонецЕсли; 
			
		КонецЦикла;
		
		// Пересчитаем существующие курсы в новую валюту учета
		Если ЗначениеЗаполнено(ПрежняяВалютаУчета) Тогда
			РаботаСКурсамиВалют.ПересчитатьЗагруженныеКурсыВалют(СписокВалютДляПересчета, ПрежняяВалютаУчета, Дата(2012,1,1), КонецГода(ТекущаяДата()));
		КонецЕсли; 		
	КонецЕсли;
	
КонецПроцедуры
