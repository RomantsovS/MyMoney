#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	// Подставим значения по умолчанию в ДанныеЗаполнения
	Если ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Тогда
		ДанныеЗаполнения = Новый Структура;
	КонецЕсли; 
	Если НЕ ДанныеЗаполнения.Свойство("БазоваяВалюта") Или НЕ ЗначениеЗаполнено(ДанныеЗаполнения.БазоваяВалюта) Тогда
		ДанныеЗаполнения.Вставить("БазоваяВалюта", Константы.ВалютаУчета.Получить());
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("Валюта") И ЗначениеЗаполнено(ДанныеЗаполнения.Валюта) 
			И (НЕ ДанныеЗаполнения.Свойство("Кратность") Или НЕ ЗначениеЗаполнено(ДанныеЗаполнения.Кратность)) Тогда
		ДатаКурса = ?(ДанныеЗаполнения.Свойство("Период") И ЗначениеЗаполнено(ДанныеЗаполнения.Период), ДанныеЗаполнения.Период, ТекущаяДата());
		КурсИКратность = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ДанныеЗаполнения.Валюта, ДатаКурса, ДанныеЗаполнения.БазоваяВалюта);
	Иначе
		КурсИКратность = Новый Структура("Курс,Кратность", 0, 1);
	КонецЕсли;
	 
	Если НЕ ДанныеЗаполнения.Свойство("Кратность") Или НЕ ЗначениеЗаполнено(ДанныеЗаполнения.Кратность) Тогда
		ДанныеЗаполнения.Вставить("Кратность", КурсИКратность.Кратность);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	// Формируем представление курса Для отображения в списках
	// Представление должно быть записано независимо от загрузки - в источниках данных его нет
	Для Каждого ЗаписьНабора Из ЭтотОбъект Цикл
		
		ЗаписьНабора.ПредставлениеКурса = Формат(ЗаписьНабора.Курс, "ЧДЦ=4; ЧН=0; ЧГ=") 
					+ " " + Строка(ЗаписьНабора.БазоваяВалюта);
		Если ЗаписьНабора.Кратность > 1 Тогда
			ЗаписьНабора.ПредставлениеКурса = ЗаписьНабора.ПредставлениеКурса 
					+ " за " + Формат(ЗаписьНабора.Кратность, "ЧДЦ=; ЧН=??; ЧГ=")
					+ " " + Строка(ЗаписьНабора.Валюта);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ОбменДанными.Загрузка Тогда
		ОбменДанными.Получатели.Очистить();
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
