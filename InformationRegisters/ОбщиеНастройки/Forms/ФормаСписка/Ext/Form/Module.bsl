
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	
	Если Параметры.Свойство("КлючОбъекта") Тогда 
		КлючОбъекта = Параметры.КлючОбъекта;
		
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КлючОбъекта");
		ЭлементОтбора.ПравоеЗначение = КлючОбъекта;
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.Использование = Истина;
		
		Элементы.КлючОбъекта.Видимость = Ложь;
		
		Элементы.Список.ПодчиненныеЭлементы.КлючОбъекта.Видимость = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("РежимВыбора") Тогда 
		Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КлючОбъекта = СписокВыборНаСервере(ВыбраннаяСтрока);
	
	Если Поле = Элементы.КлючНастроекОсновная Тогда 
		ОсновнаяПриИзмененииНаСервере(КлючОбъекта);
		
		Элементы.Список.Обновить();
	Иначе
		Закрыть(КлючОбъекта);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КлючОбъекта = СписокВыборНаСервере(Значение);
	
	Закрыть(КлючОбъекта);
	
КонецПроцедуры

&НаСервере
Функция СписокВыборНаСервере(ВыбраннаяСтрока)
	
	Возврат ВыбраннаяСтрока.КлючНастроек;
	
КонецФункции

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	
	СписокПередУдалениемНаСервере(Элемент.ТекущиеДанные.КлючНастроек);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПередУдалениемНаСервере(КлючНастроек)
	
	КлючОбъект = КлючНастроек.ПолучитьОбъект();
	Если КлючОбъект <> Неопределено Тогда 
		КлючОбъект.Удалить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОсновнаяПриИзмененииНаСервере(КлючОбъекта)
	
	Если Не КлючОбъекта.Основная Тогда		
		Об = КлючОбъекта.получитьОбъект();
		Об.Основная = Истина;
		Об.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти